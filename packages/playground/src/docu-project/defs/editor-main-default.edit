/* This file contain the default editor definition. */

editor default

boolean [YES | NO] // are the strings used to display a boolean value

referenceSeparator [:] // is the string that separates the names in a path name, e.g. pack1:cls3:part

// both modelunits show a single concept
Part {[ ${self.part} ]}
Product {[ ${self.product} ]}

BaseProduct {[
    Base Products ${name} for ${theme}
        ${parts}
]}

InsurancePart{
[
    Insurance Part ${self.name}
        risk assessment: ${self.statisticalRisk}
        maximum payout: ${self.maximumPayOut}
        is approved: ${self.isApproved}
]
}

InsuranceProduct {[
    Insurance Product ${name} ( public name: ${productName} )
        Themes: ${themes horizontal separator[, ]}
        Premium: ${advertisedPremium} per ${nrPremiumDays}
        Insured risks:
            ${parts vertical terminator [;]}
        Calculation
            [? Risk adjusted by = ${riskAdjustment} ]
            calculated premium: ${calculation}
        Helper functions:
            ${helpers vertical}
]}

CalcFunction {
    [
        ${name} ( ${parameters horizontal separator[,]} ): ${declaredType} {
            ${body}
        }
    ]
}
Parameter {
     [${name} : ${declaredType}]
}

// No need for projection for DocuType and its implementors, they
// are only used as references, so their names suffice.

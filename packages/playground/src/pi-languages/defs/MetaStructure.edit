editor default

// concept PiModelDescription {
//    name: identifier;
//    units: PiUnitDescription[];
//}
PiModelDescription {
    [
    model ${name} {
        ${properties vertical terminator [;]}
    }
    ]
}

PiUnitDescription {
    [
    modelunit ${name} {
        ${properties vertical terminator [;]}
        file-extension = ${fileExtension};
    }
    ]
}

PiStructureDef {
    [
    language ${self.name}
    ${self.modelConcept}
    ${self.units}
    ${interfaces}
    ${concepts}
    ]
}
//================================ other file ??? ===============
//abstract concept PiClassifier {
//    name: identifier;
//    isPublic: boolean;
//    properties: PiProperty[];
//}

PiClassifier {
    [ ${isPublic [public]} ]
}
// concept PiInterface base PiClassifier {...}
PiInterface {
    [
    [=> PiClassifier] interface ${name} [?base ${base horizontal separator [, ]}] {
        ${properties vertical terminator [;]}
    }
    ]
}
// concept PiConcept base PiClassifier {...}
PiConcept {
    [
    [=> PiClassifier] ${isAbstract [abstract]} concept ${name} [?base ${base}] [?implements ${interfaces horizontal separator [, ]}] {
        ${properties vertical terminator [;]}
    }
    ]
}
// concept PiExpressionConcept base PiConcept {...}
PiExpressionConcept {
    [
    [=> PiClassifier] ${isAbstract [abstract]} expression ${name} [?base ${base}] [?implements ${interfaces horizontal separator [, ]}] {
        ${properties vertical terminator [;]}
    }
    ]
}
// concept PiBinaryExpressionConcept base PiExpressionConcept {...}
PiBinaryExpressionConcept {
    [
    [=> PiClassifier] ${isAbstract [abstract]} binary expression ${name} [?base ${base}] [?implements ${interfaces horizontal separator [, ]}] {
        ${properties vertical terminator [;]}
        [?priority = ${priority};]
    }
    ]
}
// concept PiLimitedConcept base PiConcept {...}
PiLimitedConcept {
    [
    [=> PiClassifier] ${isAbstract [abstract]} limited ${name} [?base ${base}] [?implements ${interfaces horizontal separator [, ]}] {
        ${properties vertical terminator [;]}
        ${instances vertical }
    }
    ]
}
// the following two could be using the abstract box definition of PiProperty, but they are not
PiConceptProperty {
    [${isPublic [public]} ${isPart [reference]} ${name} ${isOptional [?]} : ${type} ${isList [\[\]]}]
}
PiPrimitiveProperty {
    [${isPublic [public]} ${name} ${isOptional [?]} : ${type} ${isList [\[\]]}]
}

PiInstance {
    [
    ${name} = {
        ${propertyDefs vertical separator [,]}
    }
    ]
}
PiPropertyInstance {
    [${property} : ${value}]
}
PiStringValue {
 [${value}]
}
PiNumberValue {
 [${value}]
}
PiBooleanValue {
 [${value}]
}

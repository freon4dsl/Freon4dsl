language Demo

model Demo {
    public name: identifier;
    public models: DemoUnit[];
}

modelunit DemoUnit {
    public name: identifier;
    public main: DemoEntity;
    public functions: DemoFunction[];
}

public concept DemoEntity  {
    name: identifier;
    attributes: DemoAttribute[];
    entAttributes: DemoAttributeWithEntityType[];
    functions: DemoFunction[];
    reference baseEntity: DemoEntity[];
}

concept DemoAttribute {
    name: identifier;
    somePrimAttr: string[];
    reference declaredType: DemoAttributeType;
}

limited DemoAttributeType  {
    name: identifier;
    extra: number;
    String = { name: "String", "extra": 199}
    Integer = { "name": "Integer", "extra": 240261}
    Boolean = { "name": "Boolean", "extra": 5479}
    ANY = { "name": "ANY", "extra": 456}
}

expression DemoAbsExpression base DemoExpression {
    expr: DemoExpression;
}

public concept DemoFunction {
    public name: identifier;
    reference declaredType : DemoAttributeType;
    expression: DemoExpression;
    parameters: DemoVariable[];
}

concept DemoAttributeWithEntityType {
    reference declaredType: DemoEntity;
    name: identifier;
}

concept DemoVariable {
    name: identifier;
    reference declaredType: DemoEntity;
}

abstract expression DemoExpression {
    appliedfeature?: AppliedFeature;
}

abstract concept AppliedFeature {
    appliedfeature?: AppliedFeature;
}

concept DemoAttributeRef base AppliedFeature {
    reference attribute: DemoAttributeWithEntityType;
}

expression DemoVariableRef base DemoExpression {
    reference variable: DemoVariable;
}

abstract expression DemoLiteralExpression base DemoExpression {}

expression DemoStringLiteralExpression base DemoLiteralExpression {
    value: string;
}

expression DemoNumberLiteralExpression base DemoLiteralExpression {
    value: string; // TODO should be changed to type number
}

expression DemoBooleanLiteralExpression base DemoLiteralExpression {
    value: string; // TODO should be changed to type boolean
}

abstract binary expression DemoBinaryExpression base DemoExpression {
    left: DemoExpression;
    right: DemoExpression;
}

binary expression DemoMultiplyExpression base DemoBinaryExpression {
    priority = 8;
}

binary expression DemoPlusExpression base DemoBinaryExpression {
    priority = 4;
}

binary expression DemoDivideExpression base DemoBinaryExpression {
    priority = 8;
}

binary expression DemoAndExpression base DemoBinaryExpression {
    priority = 1;
}

binary expression DemoOrExpression base DemoBinaryExpression {
    priority = 1;
}

abstract binary expression DemoComparisonExpression base DemoBinaryExpression {
}

binary expression DemoLessThenExpression base DemoComparisonExpression {
    priority = 10;
}

binary expression DemoGreaterThenExpression base DemoComparisonExpression {
    priority = 10;
}

binary expression DemoEqualsExpression base DemoComparisonExpression {
    priority = 10;
}

expression DemoFunctionCallExpression base DemoExpression {
    reference functionDefinition: DemoFunction;
}

expression DemoIfExpression base DemoExpression {
    condition: DemoExpression;
    whenTrue: DemoExpression;
    whenFalse: DemoExpression;
}



language PiLanguage

// begin structure definition
modelunit PiStructureDef {
    name: identifier;
    modelConcept: PiModelDescription;
    units: PiUnitDescription[];
    concepts: PiConcept[];
    interfaces: PiInterface[];
    file-extension = "ast";
}

// TODO this is not in our language, but we need it here because of limited concept PiPrimitiveType
interface Type {
    name: identifier;
}
concept PiParseLocation {
    filename: string;
    column: number;
    line: number;
}
// end extras
abstract concept PiLangElement {
    location?: PiParseLocation;
    name: identifier;
    // piMetaName: string; not yet implemented
}
abstract concept PiClassifier base PiLangElement implements Type {
    reference owningLanguage: PiStructureDef;
    isPublic: boolean;
    properties: PiProperty[];
    //primProperties: PiPrimitiveProperty[];
}
concept PiModelDescription base PiClassifier {
    isPublic: boolean = true;
}
concept PiUnitDescription base PiClassifier {
    fileExtension: string = "";
}
concept PiInterface base PiClassifier {
    reference base: PiInterface[];
}
concept PiConcept base PiClassifier {
    isAbstract: boolean;
    reference base?: PiConcept;
    reference interfaces: PiInterface[];
}
concept PiExpressionConcept base PiConcept {
}
concept PiBinaryExpressionConcept base PiExpressionConcept {
    left: PiExpressionConcept;
    right: PiExpressionConcept;
    priority: number;
}
concept PiLimitedConcept base PiConcept {
    instances: PiInstance[];
}
abstract concept PiProperty base PiLangElement {
    isPublic: boolean;
    isOptional: boolean;
    isList: boolean;
    isPart: boolean; // if false then it is a reference property
    reference type: Type;
    // TODO see how we address the following
    reference owningConcept?: PiClassifier;
}
concept PiConceptProperty base PiProperty {
    // TODO see how we address the following
    hasLimitedType: boolean;
}
concept PiPrimitiveProperty base PiProperty {
    isStatic: boolean;
	initialValue: PiPrimitiveValue;
	initialValueList: PiPrimitiveValue[];
}

concept PiInstance base PiLangElement {
    reference concept: PiLimitedConcept;
    propertyDefs: PiPropertyInstance[];
}
concept PiPropertyInstance base PiLangElement {
    reference owningInstance: PiInstance;
    reference property: PiProperty;
    value: PiPrimitiveValue;
    valueList: PiPrimitiveValue[];
}
// the following two classes are only used in the typer and validator definitions
concept PiFunction base PiLangElement {
    reference language: PiStructureDef;
    formalparams: PiParameter[];
    reference returnType: Type;
}

concept PiParameter base PiLangElement {
    reference type: Type;
}
// the basic types in the pi-languages
abstract concept PiPrimitiveValue {
}
concept PiStringValue base PiPrimitiveValue {
    value: string;
}
concept PiNumberValue base PiPrimitiveValue {
    value: number;
}
concept PiBooleanValue base PiPrimitiveValue {
    value: boolean;
}

limited PiPrimitiveType implements Type
{ string; boolean; number; identifier; }

// end structure definition
// begin expressions definition
abstract concept PiLangExp base PiLangElement {
    sourceName: string;							        // either the 'XXX' in "XXX.yyy" or 'yyy' in "yyy"
//    appliedFeature: PiLangAppliedFeatureExp;	        // either the 'yyy' in "XXX.yyy" or 'null' in "yyy"
    reference referredElement: PiLangElement;            // refers to the element called 'sourceName'
//    location: ParseLocation;                            // holds start and end in the parsed file
    reference language: PiStructureDef;                           // the language for which this expression is defined
}

concept PiLangSimpleExp base PiLangExp {
    value: number;
}

concept PiLangSelfExp base PiLangExp {
//    reference referredElement: PiConcept;
}

concept PiInstanceExp base PiLangExp {
//    reference referredElement: PiInstance;
}

concept PiLangConceptExp base PiLangExp {
//    reference referredElement: PiConcept;
}

concept PiLangAppliedFeatureExp base PiLangExp {
    reference sourceExp: PiLangExp;
//    reference referredElement: PiProperty;
}

concept PiLangFunctionCallExp base PiLangExp {
    //sourceName: string; 			// in typer: name can only be 'commonSuperType', in validator: only 'conformsTo' and 'equalsType'
    actualParams: PiLangExp[];
    returnValue: boolean;
//    reference referredElement: PiFunction;
}
// end expressions definition

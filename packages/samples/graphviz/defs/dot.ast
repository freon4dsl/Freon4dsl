language graphviz_dot

model xx {
    name: identifier;
    units: unitA[];
}
modelunit unitA {
    name: identifier;
    graph: AbstractGraph;
}
abstract concept AbstractGraph {
    name: identifier;
    strict: boolean;
    statements: Statement[];
}
concept Graph base AbstractGraph {
    isDiGraph: boolean;
}
// statements
abstract concept Statement {
}
concept NodeStatement base Statement {
    node: NodeExp;
    attributes?: Attr_List;
}
concept EdgeStatement base Statement {
    edge: EdgeExp;
    attributes?: Attr_List;
}
concept SubgraphStatement base Statement {
    subgraph: SubgraphExp;
    attributes?: Attr_List;    
}
concept AttrStatement base Statement {
    type: AttrType;
    attributes: Attr_List[];
}
// expressions
abstract expression EdgeEnd {
}
abstract binary expression EdgeExp base EdgeEnd {
    left: EdgeEnd;
    right: EdgeEnd;
    priority = 8;
}
binary expression DirectedEdgeExp base EdgeExp {
    priority = 8;
}
binary expression UnDirectedEdgeExp base EdgeExp {
    priority = 8;
}
expression NodeExp base EdgeEnd {
    name: identifier;
    port?: Port;
    reference node?: Node;
}
expression SubgraphExp base EdgeEnd {
    name: identifier;
    statements: Statement[];
    reference subgraph?: Subgraph;
}

// attribute lists
concept Attr_List {
    content: A_List[];
}
concept A_List {
    attrs: SingleAttr[];
}
concept SingleAttr base Statement {
    name: identifier;
    value: AttrValue;
}
concept AttrValue {
     stringValue: string;
     numberValue: number;
     booleanValue: boolean;
}

// underlying concepts
concept Subgraph {
    name: identifier;
}
concept Node {
    name: identifier;
    port?: Port;
}
concept Edge {
}
concept Port {
    name: identifier;
    compass_point?: CompassPoint;
}

// limited concepts
limited AttrType {
    GRAPH; NODE; EDGE;
}

limited CompassPoint {
        north = { name: "n" }
        northeast = { name: "ne" }
        east = { name: "e" }
        southeast = { name: "se" }
        south = { name: "s" }
        southwest = { name: "sw" }
        west = { name: "w" }
        northwest = { name: "nw" }
        central = { name: "c" }
        dash = { name: "-" }
}

editor default

boolean [RIGHT | WRONG] // are the strings used to display a boolean value

referenceSeparator [.] // is the string that separates the names in a path name, e.g. pack1.cls3.part

Attribute {
    [ ${name} : ${declaredType} ]
}
Entity {
    [
        entity ${name} [? base ${baseEntity}] {
            ${construc}
            list of references: ${listOfReferences}
            ${attributes }
            ${methods vertical }
        }
    ]
    trigger = "entity"
}
ExampleUnit {
        [
        unit ${name} {
            ${entities vertical }
        unit level methods:
            ${methods vertical}
        }
        ]
}
Method {
    [
        method ${name} ( ${parameters table rows} ): ${declaredType} {
            ${body}
        }
    ]
}
Parameter {
     [${name} : ${declaredType}]
    table [${name} | ${declaredType}]
}
MethodCallExpression {
    [CALL ${methodDefinition} ( ${args horizontal separator[,] } )]
    referenceShortcut = ${methodDefinition}
    trigger = "method"
}
SumExpression {
    [sum from ${variable} = ${from} to ${to} of ${body}]
    trigger = "sum"
}
GroupedExpression {
    [( ${inner} )]
}
LoopVariableRef {
    [ ${variable} ]
}
LoopVariable {
    [ ${name} ]
    trigger = "var"
}
NumberLiteralExpression {
    [${value}]
}
StringLiteralExpression {
    ['${value}']
    trigger = "'"
}
PlusExpression {
    symbol = "+"
}
MultiplyExpression {
    symbol = "*"
}
DivideExpression {
    symbol = "/"
}
LessThenExpression {
    symbol = "<"
}
OrExpression {
    symbol = "or"
}
AndExpression {
    symbol = "and"
}
GreaterThenExpression {
    symbol = ">"
}
EqualsExpression {
    symbol = "=="
}
BooleanLiteralExpression {
    [ ${value} ]
    trigger = "boolean"
}
AttributeRef {
    [ ${attribute} ]
    trigger = "attribute"
    referenceShortcut = ${attribute}
}

ParameterRef {
    [ ${parameter} [? . ${appliedfeature}] ]
    referenceShortcut = ${parameter}
}
IfExpression {
        [
        if (${condition}) then
            ${whenTrue}
        else
            ${whenFalse}
        endif
        ]
    trigger = "if"
}
AbsExpression {
    [ \| ${expr} \| ]
}

//IfExpression {
//        [
//        ${ condition } ? ${ whenTrue } : ${whenFalse}
//        ]
//    trigger = "if"
//}
